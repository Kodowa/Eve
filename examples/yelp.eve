-----------------
Handle Events
-----------------

click on a restaurant or pin, show a restaurant
  app = [@app]
  restaurant = if [#click element: [#yelp-restaurant-list-element restaurant]] then restaurant
               else if [#click element: [#yelp-restaurant-pin restaurant]] then restaurant
  save
    app.selected := restaurant
    app.content := "restaurant"

click on yelp logo, show map
  app = [@app]
  [#click element: [#yelp-logo]]
  save
    app.content := "map"

-----------------
Draw the page
-----------------

draw the selected restaurant
  root = [@yelp-root]
  restaurant = [#restaurant name image]
  [@app content: "restaurant", selected: restaurant]
  maintain
    root.children += [#div class: "restaurant-info", children:
                        [#div class: "info-header", children:
                          [#h2 text: name]
                          [#div children:
                            [#img src: image]]]]

draw the map pane
  root = [@yelp-root]
  [@app content: "map"]
  [#restaurant street city state zip]
  [#address-to-latlon street city state zip lat long]
  maintain
    root.children += [#map class: "map", pins: [#yelp-restaurant-pin lat lon]]

draw the restaurant list
  restaurant = [#restaurant name rating]
  root = [@yelp-root]
  star-image = "star{rating}.png"
  maintain
    root.children += [#div class: "restaurant-list-container" children:
                      [#div class: "restaurant-list-header" children: [#h1 text: "Restaurants"]]
                      [#div class: "restaurant-list-elements" children:
                        [#div #yelp-restaurant-list-element, class: "restaurant-list-element",
                          restaurant, name, star-image, children:
                          [#h1 text: name]
                          [#img src: star-image]]]]

draw the main page
  maintain
    [#div @yelp-root class: "yelp-root", children:
      [#div class: "header", children: [#h1 #yelp-logo text: "Yelp"]]]
